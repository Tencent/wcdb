apply plugin: 'com.android.library'
apply plugin: 'maven-publish'


android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        minSdkVersion 12
        targetSdkVersion 25
        versionCode 1
        versionName rootProject.VERSION

        externalNativeBuild {
            ndkBuild {
                abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                arguments '-j2'

                def hasBuildInfo = false;
                if (rootProject.hasProperty("WX_BUILD_REVISION"))
                    hasBuildInfo = arguments.add('WX_BUILD_REVISION=\'' + rootProject.WX_BUILD_REVISION + '\'')
                if (rootProject.hasProperty("WX_BUILD_BRANCH"))
                    hasBuildInfo = arguments.add('WX_BUILD_BRANCH=\'' + rootProject.WX_BUILD_BRANCH + '\'')
                if (rootProject.hasProperty("WX_BUILD_TIME"))
                    hasBuildInfo = arguments.add('WX_BUILD_TIME=\'' + rootProject.WX_BUILD_TIME + '\'')
                if (rootProject.hasProperty("WX_BUILD_JOB"))
                    hasBuildInfo = arguments.add('WX_BUILD_JOB=\'' + rootProject.WX_BUILD_JOB + '\'')
                if (hasBuildInfo) {
                    arguments.add('WX_BUILD_VERSION=\'' + rootProject.VERSION_NAME + '\'')
                    arguments.add('WITH_BUILD_INFO=1')
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            jniDebuggable true
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            aidl.srcDirs = ['src']
            jni.srcDirs = ['../jni']
            jniLibs.srcDirs = []
        }
    }

    externalNativeBuild {
        ndkBuild {
            path rootProject.file('Android.mk')
        }
    }

    defaultPublishConfig 'release'
    publishNonDefault true

    lintOptions {
        disable 'OldTargetApi', 'GradleDependency'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

android.libraryVariants.all { variant ->

    def varName = variant.name
    def cvarName = varName.capitalize()

    // emit Javadoc generation task
    def genJavadocTask = task("generate${cvarName}Javadoc", type: Javadoc) {
        description "Generates Javadoc for ${varName}."
        source = variant.javaCompile.source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)

        title = null

        options {
            memberLevel = JavadocMemberLevel.PUBLIC

            doclet = 'com.google.doclava.Doclava'
            docletpath = [rootProject.file('gradle/doclava-1.0.6.jar')]

            addStringOption('title', 'WCDB for Android')
            addStringOption('hdf project.name', 'WCDB for Android')
            //addStringOption('federate Android', 'http://d.android.com/reference')
            //addStringOption('federationxml Android', rootProject.file('gradle/android-10.xml').path)
        }

        dependsOn "generate${cvarName}Sources"
    }

    // emit Javadoc bundling task
    task("bundle${cvarName}Javadoc", type: Jar) {
        classifier = 'javadoc'
        from genJavadocTask.destinationDir
        dependsOn genJavadocTask
    }

    // emit source bundling task
    task("bundle${cvarName}Sources", type: Jar) {
        classifier = 'sources'
        from variant.javaCompile.source
        dependsOn "generate${cvarName}Sources"
    }

    // emit native symbols bundling task
    def bundleSymTask = task("bundle${cvarName}NativeSymbols", type: Zip) {
        def nativeBuildTask = project.tasks.findByName("externalNativeBuild${cvarName}")

        from nativeBuildTask.getObjFolder()
        include '**/*.so'
        dependsOn nativeBuildTask
    }

    def bundleTask = project.tasks.findByName("bundle${cvarName}")
    if (varName != 'release') {
        bundleTask.classifier = varName
        bundleSymTask.classifier = "${varName}Symbols"
    } else {
        bundleTask.classifier = ''
        bundleSymTask.classifier = 'symbols'
    }
}

apply from: 'bintray.gradle'

if (file('local.gradle').isFile())
    apply from: 'local.gradle'
