apply plugin: 'com.android.library'
apply plugin: 'maven-publish'


android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName rootProject.VERSION

        externalNativeBuild {
            ndkBuild {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                arguments '-j2'

                if (rootProject.hasProperty("USE_GCC")) {
                    if (rootProject.USE_GCC == '1')
                        arguments.add('USE_GCC=1')
                }

                def hasBuildInfo = false
                if (rootProject.hasProperty("WX_BUILD_REVISION"))
                    hasBuildInfo = arguments.add('WX_BUILD_REVISION=\'' + rootProject.WX_BUILD_REVISION + '\'')
                if (rootProject.hasProperty("WX_BUILD_BRANCH"))
                    hasBuildInfo = arguments.add('WX_BUILD_BRANCH=\'' + rootProject.WX_BUILD_BRANCH + '\'')
                if (rootProject.hasProperty("WX_BUILD_TIME"))
                    hasBuildInfo = arguments.add('WX_BUILD_TIME=\'' + rootProject.WX_BUILD_TIME + '\'')
                if (rootProject.hasProperty("WX_BUILD_JOB"))
                    hasBuildInfo = arguments.add('WX_BUILD_JOB=\'' + rootProject.WX_BUILD_JOB + '\'')
                if (hasBuildInfo) {
                    arguments.add('WX_BUILD_VERSION=\'' + rootProject.VERSION_NAME + '\'')
                    arguments.add('WITH_BUILD_INFO=1')
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }

        debug {
            consumerProguardFiles 'proguard-rules.pro'
            jniDebuggable true
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            aidl.srcDirs = ['src']
            jni.srcDirs = ['../jni']
            jniLibs.srcDirs = []
        }
    }

    externalNativeBuild {
        ndkBuild {
            path rootProject.file('Android.mk')
        }
    }

    lintOptions {
        disable 'OldTargetApi', 'GradleDependency'
    }
}

android.libraryVariants.all { variant ->

    def varName = variant.name
    def cvarName = varName.capitalize()

    // emit Javadoc generation task
    def genJavadocTask = task("generate${cvarName}Javadoc", type: Javadoc) {
        description "Generates Javadoc for ${varName}."
        source = variant.javaCompile.source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)

        title = null

        options {
            memberLevel = JavadocMemberLevel.PUBLIC

            doclet = 'com.google.doclava.Doclava'
            docletpath = [rootProject.file('gradle/doclava-1.0.6.jar')]

            addStringOption('title', 'WCDB for Android')
            addStringOption('hdf project.name', 'WCDB for Android')
            //addStringOption('federate Android', 'http://d.android.com/reference')
            //addStringOption('federationxml Android', rootProject.file('gradle/android-10.xml').path)
        }

        dependsOn "generate${cvarName}Sources"
    }

    // emit Javadoc bundling task
    task("bundle${cvarName}Javadoc", type: Jar) {
        classifier = 'javadoc'
        from genJavadocTask.destinationDir
        dependsOn genJavadocTask
    }

    // emit source bundling task
    task("bundle${cvarName}Sources", type: Jar) {
        classifier = 'sources'
        from variant.javaCompile.source
        dependsOn "generate${cvarName}Sources"
    }

    // emit native symbols bundling task
    task("bundle${cvarName}NativeSymbols", type: Zip) {
        def nativeBuildTask = project.tasks.findByName("externalNativeBuild${cvarName}")

        classifier = (varName == 'release') ? 'symbols' : "${varName}Symbols"
        from nativeBuildTask.getObjFolder()
        include '**/*.so'
        dependsOn nativeBuildTask
    }
}

apply from: 'bintray.gradle'

if (file('local.gradle').isFile())
    apply from: 'local.gradle'
